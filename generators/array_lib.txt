@name 
@inputs 
@outputs 
@persist Chip:table
@trigger 

if (first()){
    #include "github/lib/bootstrap_builder"
    #include "github/lib/compares"
            
    Chip = bsInitChip()
    Chip:bsSetName("array_lib")
    Chip:bsAddInclude("github/lib/compares")
}

Chip:bsUseSections()


foreach(I, ComparableType:string = array("number", "string")){
    if (Chip:bsDefSection("sort<type>By" + ComparableType:capitalize())){
        SectionLines = array()
        
        foreach(J, ArrayType:string = array("number", "string", "vector", "entity", "angle")){
            FunctionName = "sort" + ArrayType:capitalize() + "sBy" + ComparableType:capitalize()
            SectionLines = SectionLines:add(array(
                "",
                "function array array:" + FunctionName + "(Func:string, Desc:number){",
                tab() + "local SortedArr = array()",
                tab() + "foreach(I, A:" + ArrayType + " = This){",
                tab(2) + "local Inserted = 0",
                tab(2) + "foreach(J, B:" + ArrayType + " = SortedArr){",
                tab(3) + "local AVal = (Desc ? Func(B)[" + ComparableType + "] : Func(A)[" + ComparableType + "])",
                tab(3) + "local BVal = (Desc ? Func(A)[" + ComparableType + "] : Func(B)[" + ComparableType + "])",
                tab(3) + "if (AVal:compare(BVal) < 0){",
                tab(4) + "SortedArr:insert" + ArrayType:capitalize() + "(J, A)",
                tab(4) + "Inserted = 1",
                tab(4) + "break",
                tab(3) + "}",
                tab(2) + "}",
                tab(2) + "if (Inserted == 0){",
                tab(3) + "SortedArr:push" + ArrayType:capitalize() + "(A)",
                tab(2) + "}",
                tab() + "}",
                tab() + "return SortedArr",
                "}",
                "function array array:" + FunctionName + "(Func:string){",
                tab() + "return This:" + FunctionName + "(Func, 0)",
                "}"
            ))
        }
        
        Chip:bsAddLinesFirst(SectionLines)
    }
}

if (Chip:bsDefSection("Tests")){
    Chip:bsAddLines(array(
        "foreach(I, N:number = array(4, 5, 3, 1, 2):sortNumbersByNumber(\"int\", 1)){",
        tab() + "print(N)",
        "}"
    ))
    
}

Chip:bootstrap()
